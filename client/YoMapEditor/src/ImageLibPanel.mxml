<?xml version="1.0" encoding="utf-8"?>
<InfoPanel xmlns="*" xmlns:mx="http://www.adobe.com/2006/mxml" width="320" height="420" creationComplete="initPanel(event)" titleStyleName="panelTitle" title="地图元件库">
	<mx:Script>
		<![CDATA[
			import com.heptafish.mapeditor.events.MapEditorEvent;
			import com.heptafish.mapeditor.events.MapEditorEventDispather;
			import com.heptafish.mapeditor.utils.ImageLoader;
			import com.heptafish.mapeditor.utils.MapEditorConstant;
			
			import mx.controls.Alert;
			import mx.core.WindowedApplication;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;

			private var _buildingInfo:BuildingInfo;
			
			private var _configXml:XML;//配置XML文件
			
			private var _configFile:File;//配置XML文件
			
			private var _libHome:File;
			
			private var _itemId:int = 0;
			
			private var _loader:ImageLoader;
			
			private var _imagesFilter:FileFilter = new FileFilter("image", "*.jpg;*.gif;*.png");//文件类型过滤
			
			private var _newImageLibPanel:NewImageLibPanel;//新建目录弹出窗口
			
			private var _model:MapModel;
			
			private function initPanel(evet:Event):void{
				_buildingInfo = new BuildingInfo();
				
				_loader = new ImageLoader();
				_loader.addEventListener(Event.COMPLETE, __setBrushBitmapData);
				
				_libHome = MapEditorConstant.COMPONENT_LIB_HOME;
				_configFile = _libHome.resolvePath("config.xml");
				if(_configFile.exists == false)
				{
					_configXml = <lib><itemId>0</itemId></lib>;
					writeConfig();
				}
				else
				{
					var fs:FileStream = new FileStream();
					fs.open(_configFile, FileMode.READ);
					_configXml = new XML(fs.readUTFBytes(fs.bytesAvailable));
				}
				libTree.directory = _libHome;
				
				MapEditorEventDispather.addEventListener(MapEditorEvent.EDIT_BUILD_SUBMIT, __editBuildFinish);
			}
			
			//弹出输入新建目录名称panel
			private function newLib(evet:Event):void{
				_newImageLibPanel = NewImageLibPanel(PopUpManager.createPopUp(this, NewImageLibPanel, true));
				PopUpManager.centerPopUp(_newImageLibPanel);
				_newImageLibPanel.addEventListener(MapEditorEvent.NEW_IMAGELIB_SUBMIT, __createNewLib); 
			}
			
			//新建目录
			private function __createNewLib(evet:MapEditorEvent):void{
				_newImageLibPanel.removeEventListener(MapEditorEvent.NEW_IMAGELIB_SUBMIT, __createNewLib);
				var newLibName:String = evet.data.toString();
				var file:File = _libHome.resolvePath(newLibName);
				file.createDirectory();
				PopUpManager.removePopUp(_newImageLibPanel);
				libTree.refresh();
			}
			//删除提示
			private function delLib(evet:Event):void{
				var selectFile:File = File(libTree.selectedItem);
				Alert.okLabel = "确定";
				Alert.cancelLabel = "取消";
				if(selectFile != null){
					Alert.show("确定删除[" + selectFile.name + "]", "", Alert.OK | Alert.CANCEL, null, delItem, null, Alert.CANCEL);
				}else{
					Alert.show("请单击选择要删除的项目！");
				}
				//删除方法
				function delItem(event:CloseEvent):void{
					if (event.detail==Alert.CANCEL)
					{
	                    return;
	    			}
	                //声明选中文件：文件=文件（树形控件ID.选中的文件）
					var selectFile:File = File(libTree.selectedItem);
					var index:int;
					//如果选中文件是文件夹
					if (selectFile.isDirectory)
					{
						//删除文件夹
						selectFile.deleteDirectory(true);
					} else {
						//修改文件
						//删除 控制XML.xml文件的
						delete _configXml.item.(@file==MapEditorConstant.IMAGE_PATH + _libHome.getRelativePath(selectFile))[0];	//与上面注释代码等同
						selectFile.deleteFile();
						//把xml数据写入xml文件
						writeConfig();
						
					}
					libTree.refresh();
				}
			}
			
			/**
			 * 预览图片
			 */
			private function fileTreeChange(event:Event):void
			{
				var selectFile:File = File(libTree.selectedItem);
				if(selectFile.isDirectory)
				{
					
				}
				else
				{
					imgPrvImg.source = selectFile.nativePath;
					var rp:String = _libHome.getRelativePath(selectFile);//选择图片的相对路径
					for each (var item:XML in _configXml.item) 
					{
						if(item.@file.toString() == MapEditorConstant.IMAGE_PATH + rp){
							_buildingInfo.id = item.@id;
							_buildingInfo.name = selectFile.name;
							_buildingInfo.type = item.@type;
							_buildingInfo.path = selectFile.nativePath;
							_buildingInfo.hinder = item.@walkable;
							_buildingInfo.originX = item.@originX;
							_buildingInfo.originY = item.@originY;
							_buildingInfo.cellWidth = _model.cellWidth;
							_buildingInfo.cellHeight = _model.cellHeight;
						}
					}
					//设置画笔
					_loader.load(selectFile.url);
				}
			}
			
			//设置主界面画笔图像
			private function __setBrushBitmapData(e:Event):void
			{
				_buildingInfo.bitmapData = e.target.data;
				
				MapEditorEventDispather.dispatchEvent(MapEditorEvent.BUILDING_INFO_UPDATE, _buildingInfo);
			}
			
			//导入图片
			private function impotPic(evet:Event):void{
			    if (libTree.selectedItem == null)
			    {
			    	Alert.show("请先选择要导入图片的目录!");
			    }else{
					var directory:File = File.applicationDirectory;
				    directory.browseForOpenMultiple("选择图片文件", [_imagesFilter]);
				    directory.addEventListener(FileListEvent.SELECT_MULTIPLE, addPic);
			    }
			}
			
			//加入图片
			private function addPic(event:FileListEvent):void 
			{
				var fa:Array = event.files;
				_itemId = _configXml.itemId[0];
			    for each(var openFile:File in fa)
				{
				    var nowFile:File = File(libTree.selectedItem);
				    var destFile:File;
				    if(nowFile.isDirectory){
				    	destFile = nowFile.resolvePath(openFile.name)
				    }
					else
					{
				    	destFile = nowFile.parent.resolvePath(openFile.name);
				    }
					
				    if(destFile.exists)
					{
				    	Alert.show("该目录下已有 " + openFile.name + " 文件！此文件未导入！");
				    }
					else
					{
					    openFile.copyTo(destFile, false);
			    		var xmlItem:XML = <item id={_itemId} file={MapEditorConstant.IMAGE_PATH + _libHome.getRelativePath(destFile)} />;
			    		xmlItem.@type = "";
			    		_configXml.appendChild(xmlItem);
			    		_itemId++;
			    		_configXml.itemId[0] = _itemId;
				    }
			    }
				libTree.refresh();
				writeConfig();
			}
			
			//写入配置文件
			private function writeConfig():void{
			    var xmlFileStream:FileStream = new FileStream();
			    xmlFileStream.open(_configFile, FileMode.WRITE);
			    xmlFileStream.writeUTFBytes(_configXml.toXMLString());
			    xmlFileStream.close();
			}
			
			//编辑建筑
			private function editBuild(evet:Event = null):void{
				var selectFile:File = File(libTree.selectedItem);
				if(selectFile && selectFile.isDirectory)
				{
					return;
				}
				
				MapEditorEventDispather.dispatchEvent(MapEditorEvent.SHOW_EDIT_BUILD, _buildingInfo);
			}
			
			/**
			 * 编辑好建筑
			 */
			private function __editBuildFinish(e:MapEditorEvent):void
			{
				for each (var item:XML in _configXml.item) 
				{
					if(item.@id.toString() == _buildingInfo.id){
						item.@type = _buildingInfo.type;
						item.@walkable = _buildingInfo.hinder;
						item.@originX = _buildingInfo.originX;
						item.@originY = _buildingInfo.originY;
					}
				}
				writeConfig();
			}

			public function get model():MapModel
			{
				return _model;
			}

			public function set model(value:MapModel):void
			{
				_model = value;
			}

		]]>
	</mx:Script>

	<mx:Image id="imgPrvImg" width="160" height="90" autoLoad="true" scaleContent="true" x="80" y="31"/>
	<mx:FileSystemTree backgroundAlpha="0.3" id="libTree" x="10" y="129" height="206" doubleClick="{editBuild()}"  extensions="['.gif','.jpg','.png']" change="{fileTreeChange(event)}" width="280"/>
	<mx:Button id="btn_newLib" x="10" y="343" label="新建目录" click="{newLib(event)}"/>
	<mx:Button id="btn_impotImage" x="88" y="343" label="导入图片" click="{impotPic(event)}"/>
	<mx:Button id="btn_edit" x="166" y="343" label="编辑" click="editBuild(event)"/>
	<mx:Button id="btn_delLib" x="236.7" y="343" label="删除" click="delLib(event)"/>
	
</InfoPanel>
